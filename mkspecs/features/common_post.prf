################################
# Unix rpath
################################

equals (TEMPLATE, lib) {
    # Uncomment line below to supress the default RPATH
    # QMAKE_LFLAGS_RPATH =

    macx: QMAKE_LFLAGS_SONAME  = -Wl,-install_name,@rpath/
    else:unix: QMAKE_LFLAGS *= '-Wl,-rpath,\'\$$ORIGIN\''
}

equals (TEMPLATE, app) {
    # Uncomment line below to supress the default RPATH
    # QMAKE_LFLAGS_RPATH =

    # Add rpath to externals to run tests without installing.
    # DYLD_LIBRARY_PATH isn't working anymore since OS X 10.11.
    unix: QMAKE_LFLAGS *= '-Wl,-rpath,\'$$EXTERNALSDIR\''

    macx {
        contains(CONFIG, app_bundle): QMAKE_LFLAGS *= -Wl,-rpath,@executable_path/../../../../lib/
        else: QMAKE_LFLAGS *= -Wl,-rpath,@executable_path/../lib/
    }
    else:unix: QMAKE_LFLAGS *= '-Wl,-rpath,\'\$$ORIGIN/../lib\''
}

################################
# Copy data files to build dir
################################

include(copyfiles.pri)

################################
# Allow "empty" projects on OSX
################################

macx:staticlib:isEmpty(SOURCES) {
    QMAKE_AR = touch
    QMAKE_RANLIB = touch
    QMAKE_MAC_SDK.$$basename(QMAKESPEC).$${QMAKE_MAC_SDK}.QMAKE_AR = $$QMAKE_AR
    QMAKE_MAC_SDK.$$basename(QMAKESPEC).$${QMAKE_MAC_SDK}.QMAKE_RANLIB = $$QMAKE_RANLIB
}

################################
# Black libs
################################

include(libraries.pri)
